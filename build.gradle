buildscript {
    ext.kotlin_version = '1.4.10'
    ext.siteUrl = 'https://github.com/Ircover/SelectingAdapter'
    ext.gitUrl = 'https://github.com/Ircover/SelectingAdapter.git'
    ext.versionName = "1.0.0"
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
        classpath 'ru.vyarus:gradle-pom-plugin:1.3.0'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'ru.ircover.selectionmanager:core:1.1.0'
    implementation 'ru.ircover.selectionmanager:livesource:1.0.1'
}

version = versionName
group = "ru.ircover.selectionmanager"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    //noinspection GrDeprecatedAPIUsage
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "ru.ircover.selectionmanager"
            artifactId "selectingadapter"
            version versionName
            artifact sourcesJar
        }
    }
}
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'

            groupId "ru.ircover.selectionmanager"
            artifactId "selectingadapter"
            version versionName
            name "SelectingAdapter"
            url siteUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'Ircover'
                    name 'Ircover'
                    email 'Ircover@yandex.ru'
                }
            }

            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}
bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayApiKey')
    configurations = ['archives']
    pkg {
        repo = 'selection-manager'
        userOrg = user
        name = 'selectingadapter'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publish = true
        publicDownloadNumbers = true
        version {
            name = versionName
            desc = 'Gradle Bintray Plugin 1.0 final'
            vcsTag = versionName
            released  = new Date()
            gpg {
                sign = true
                passphrase = project.property('bintrayPass')
            }
        }
    }
}